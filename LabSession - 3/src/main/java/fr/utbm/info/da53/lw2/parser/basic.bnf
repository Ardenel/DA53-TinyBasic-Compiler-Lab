    code ::= lineOfCode restOfCode

    restOfCode ::= <CR> lineOfCode restOfCode
                    | { empty }

    lineOfCode ::= <NUMBER> statement
                   | statement

    statement ::=   <PRINT> expression
                    | <IF> expression relop expression <THEN> statement elsestatement
                    | <GOTO> expression
                    | <INPUT> variable
                    | <LET> variable <EQ> expression
                    | <GOSUB> expression
                    | <RETURN>
                    | <END>
                    | <REM> <STRING>

    elsestatement ::= <ELSE> statement
                      | { empty }

    relop ::= <EQ> | <NE> | <LT> | <LE> | <GT> | <GE>

    expression ::= term expression_end

    expression_end ::= <PLUS> term expression_end
                    | <MINUS> term expression_end
                    | { empty }

    term ::= factor term_end

    term_end ::= <MULTIPLY> factor term_end
              | <DIVIDE> factor term_end
              | { empty }

    factor ::= <OPARENT> expression <CPARENT>
                | variable

    variable ::= <NUMBER>
                  | <IDENTIFIER>